---
chapter_no: 30
chapter_title: オートシェイプ内の文字列を一括置換
created: 2022-04-19
updated: 2022-04-19
---
今度はオートシェイプ内の文字列を一括置換するコードです。
```
Option Explicit

Sub ReplaceTextsInShapes()
    {em:comment{' 全シェイプオブジェクトに対してループ}em}
    Dim p_shape As Variant
    For Each p_shape In ActiveWindow.Document.Shapes
        {em:comment{' シェイプオブジェクトが描画キャンバスの場合}em}
        If p_shape.Type = msoCanvas Then
            {em:comment{' 描画キャンバス内のすべてのアイテムに対してループ}em}
            Dim p_canvasShape As Variant
            For Each p_canvasShape In p_shape.CanvasItems
                {em:comment{' グループ化されている場合は再度ループ}em}
                If p_canvasShape.Type = msoGroup Then
                    Dim p_canvasGroupShape As Variant
                    For Each p_canvasGroupShape In p_canvasShape.GroupItems
                        ReplaceShapeTexts p_canvasGroupShape
                    Next p_canvasGroupShape
                Else
                    ReplaceShapeTexts p_canvasShape
                End If
            Next p_canvasShape
        
        {em:comment{' オートシェイプがグループ化されている場合}em}
        ElseIf p_shape.Type = msoGroup Then
            Dim p_groupShape As Variant
            For Each p_groupShape In p_shape.GroupItems
                ReplaceShapeTexts p_groupShape
            Next p_groupShape
            
        {em:comment{' オートシェイプが単なるシェイプオブジェクトの場合}em}
        Else
            ReplaceShapeTexts p_shape
        End If
    Next p_shape
End Sub

Private Sub ReplaceShapeTexts(ByRef x_shapes As Variant)
    {em:comment{' テキストが含まれないオートシェイプなら処理を抜ける}em}
    If Not x_shapes.TextFrame.HasText Then
        Exit Sub
    End If
    
    Dim p_range As Word.Range
    Dim p_find As Word.Find

    {em:comment{' [NOTE] Rangeオブジェジェクトから生成したFindは}em}
    {em:comment{'        検索文字が見つかる度に範囲が再構築される}em}
    Set p_range = x_shapes.TextFrame.TextRange
    Set p_find = p_range.Find
    
    {em{p_find.Text = "あ"}em}
    p_find.MatchFuzzy = False  {em:comment{' ワイルドカードを利用するときはあいまい検索をオフにする}em}
    p_find.MatchWildcards = True  {em:comment{' ワイルドカード指定で検索}em}
    {em:comment{' 検索を実行}em}
    If p_find.Execute Then
        {em:comment{' 見つかった文字を取得}em}
        Dim p_foundStr As String
        p_foundStr = p_range.Text
        
        {em:comment{' 置換を実行}em}
        {em:blue{p_range.Text = "★"}em}
    End If
End Sub
```
- まずは、`ReplaceTextsInShapes`でオートシェイプの種類を特定しています。
- `ReplaceTextsInShapes`内から、`ReplaceShapeTexts`を呼び出しています。
- 上記はオートシェイプの`"あ"`という文字列を`"★"`に置換しています。