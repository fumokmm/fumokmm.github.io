plugins {
    id "com.github.jlouns.cpe" version "0.5.0"
}

apply from: '_scripts/makeArticles.gradle'

defaultTasks "updateAll"

ext {
    // カテゴリ
    categoryList = ['it', 'life', 'reading', 'science', 'economy']
}

task clean(type: Delete) {
    group = '記事管理'
    description = '.workをクリアします。'

    doFirst {
        categoryList.each { category ->
            delete "${projectDir}/.work/articles/${category}"
        }
    }
}

task copyArticles(type: Copy) {
    group = '記事管理'
    description = '.workの記事をcollectionsに上書きコピーします。'
    dependsOn "makeArticles"
    mustRunAfter 'clean' // cleanが同時指定されている場合、先にcleanを実行すること

    categoryList.each { category ->
        // サブカテゴリのindex.md
        //   → collections配下で管理して二重管理を防ぐ [210426]
        // from("${projectDir}/_articles/${category}/") {
        //     into "_${category}_articles/"
        //     include "*/*.md"
        // }

        // サブカテゴリのメモ(makeArticlesにて結合済み)
        from("${projectDir}/.work/articles/${category}/") {
            into "_${category}_articles/"
        }
    }
    into "${projectDir}/collections"
}

task checkUpdated(type: CrossPlatformExec) {
    group = '記事管理'
    description = 'check_update.rbを実行して、更新日付を調整します。'
    mustRunAfter 'copyArticles'

    commandLine 'ruby', "${projectDir}/_scripts/check_updated.rb"
}

task update(dependsOn: ['makeArticles', 'copyArticles']) {
     group = '記事管理'
     description = 'makeArticles -> copyArticles の順に実行'
}
task updateAll(dependsOn: ['makeArticles', 'copyArticles', 'checkUpdated']) {
     group = '記事管理'
     description = 'makeArticles -> copyArticles -> checkUpdate の順に実行'
}
